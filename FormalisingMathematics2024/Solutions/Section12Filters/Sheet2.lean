/-
Copyright (c) 2023 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author : Kevin Buzzard
-/
import Mathlib.Tactic
-- imports all the Lean tactics
import Mathlib.Order.Filter.Basic


/-!

# The order (â‰¤) on filters

We think of filters as generalised subsets, and just as subsets are partially ordered
by `âŠ†`, filters are partially ordered too, by `â‰¤`. Recall that a subset `X : set Î±`
of `Î±` gives rise to a principal filter `ð“Ÿ X : filter Î±`, and we definitely
want `X âŠ† Y â†” ð“Ÿ X â‰¤ ð“Ÿ Y` so let's think about how this should work. If `F` and `G`
are filters, then `F â‰¤ G` should mean "the generalised subset `F` is contained
in the generalised subset `G`", so it should mean "if a normal subset of Î± contains
`G` then it contains `F`", so it should mean `G.sets âŠ† F.sets`, which is in fact
the definition. Note that the smaller the filter `F`, the bigger the collection
`F.sets`, because `F` is contained in more sets!

In the `Filter` namespace there's a lemma


Let's formalise this. Show that ð“Ÿ S â‰¤ ð“Ÿ T â†” S âŠ† T.
Note that this is called `principal_mono` in mathlib but
there's no harm in proving it yourself.

Some helpful lemmas (all in the `Filter` namespace):

`mem_principal : T âˆˆ ð“Ÿ S â†” S âŠ† T`
`mem_principal_self S : S âˆˆ ð“Ÿ S`
`le_def : F â‰¤ G â†” âˆ€ (S : Set Î±), S âˆˆ G â†’ S âˆˆ F`

-/

namespace Section12Sheet2Solutions

variable {Î± : Type}

open Filter Set
-- so we don't keep having to type `Filter.le_def` and `Set.Subset.trans` etc

open scoped Filter
-- for ð“Ÿ notation

example (S T : Set Î±) : ð“Ÿ S â‰¤ ð“Ÿ T â†” S âŠ† T := by
  constructor
  Â· intro h
    rw [le_def] at h
    have hT : T âˆˆ ð“Ÿ T := mem_principal_self T
    specialize h T hT
    rwa [mem_principal] at h
  Â· intro hST
    rw [le_def]
    intro X hX
    rw [mem_principal] at hX âŠ¢
    exact Subset.trans hST hX

-- Here's another useful lemma about principal filters.
-- It's called `le_principal_iff` in mathlib but why
-- not try proving it yourself?
example (F : Filter Î±) (S : Set Î±) : F â‰¤ ð“Ÿ S â†” S âˆˆ F := by
  rw [le_def]
  constructor
  Â· intro h
    apply h
    exact mem_principal_self S
  Â· intro hSF X hX
    rw [mem_principal] at hX
    exact mem_of_superset hSF hX

/-

## Filters are a complete lattice

First I claim that if Fáµ¢ are a bunch of filters, indexed by `i : I`, then
the intersection of `Fáµ¢.sets` is also a filter. Let's check this.

-/
def lub {I : Type} (F : I â†’ Filter Î±) : Filter Î± where
  sets := {X | âˆ€ i, X âˆˆ F i}
  univ_sets := by
    intro i
    apply univ_mem
  sets_of_superset := by
    intro S T hS hST i
    apply mem_of_superset _ hST
    apply hS
  inter_sets := by
    intro S T hS hT i
    exact inter_mem (hS i) (hT i)

/-

Now let's check that this is a least upper bound for the Fáµ¢! We check the
two axioms.

-/
-- it's an upper bound
example (I : Type) (F : I â†’ Filter Î±) (i : I) : F i â‰¤ lub F := by
  intro S hS
  apply hS

-- it's â‰¤ all other upper bounds
example (I : Type) (F : I â†’ Filter Î±) (G : Filter Î±) (hG : âˆ€ i, F i â‰¤ G) :
    lub F â‰¤ G := by
  intro S hS i
  apply hG
  exact hS

/-

Just like it's possible to talk about the topological space generated
by a collection of subsets of `Î±` -- this is the smallest topology
for which the given subsets are all open -- it's also possible to talk
about the filter generated by a collection of subsets of `Î±`. One
can define it as the intersection of all the filters that contain your
given collection of subsets (we just proved above that this is a filter).
This gives us a definition of greatest lower bound for filters too.

-/
-- greatest lower bound of filters Fáµ¢ is the least upper bound of the filters G whose `sets`
-- contain all of the `Fáµ¢.sets`
def glb {I : Type} (F : I â†’ Filter Î±) : Filter Î± :=
  lub fun G : {G : Filter Î± | âˆ€ i, (F i).sets âŠ† G.sets} => G.1

-- it's a lower bound
example (I : Type) (F : I â†’ Filter Î±) (i : I) : glb F â‰¤ F i := by
  rintro S hS âŸ¨G, hGâŸ©
  dsimp
  apply hG _ hS

-- it's â‰¥ all other lower bounds
example (I : Type) (F : I â†’ Filter Î±) (G : Filter Î±) (hG : âˆ€ i, G â‰¤ F i) :
    G â‰¤ glb F := by
  intro S hS
  unfold glb at hS
  dsimp at hS
  unfold lub at hS
  dsimp at hS
  specialize hS âŸ¨G, _âŸ©
  Â· exact hG
  Â· exact hS

end Section12Sheet2Solutions
